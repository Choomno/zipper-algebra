LoadPackage("gbnp");
SetInfoLevel(InfoGBNP, 2);
SetInfoLevel(InfoGBNPTime,2);
SetRecursionTrapInterval(50000);

# in principle we want the coefficient ring to be
# R = Q(q,t) (too expensive)
# R := FunctionField(Rationals, ["q","t"]);
# indeterminates := IndeterminatesOfFunctionField(R);
# q := indeterminates[1];
# t := indeterminates[2];

# our actual coefficient ring (edit pStr; p is derived from it so the two stay in sync)
pStr := "2^61-1";
p := EvalString(pStr);
R := GF(p);
q := 3*One(R);
t := 5*One(R);

# strands
n := 4;

# logfile name encodes (n, p, q, t)
LogTo(Concatenation("logfile_zipper","-", String(n), "-(", pStr, ")-",
                    String(Int(q)), "-", String(Int(t)), ".txt"));


# define free algebra
# xs are generators, ys their inverses
generators := Concatenation(
    List([1..n-1], i -> Concatenation("x", String(i))),
    List([1..n-1], i -> Concatenation("y", String(i)))
);
F := FreeAssociativeAlgebraWithOne(R, generators);
generators := GeneratorsOfAlgebra(F);
one := generators[1];
xs := generators{[2..n]};;
ys := generators{[n+1..2*n-1]};;


# define relations
relations := [];


# add invertibility relation for generators
for i in [1..n-1] do
  Add(relations, xs[i]*ys[i] - one);
  Add(relations, ys[i]*xs[i] - one);
od;


# add far commutativity and braid relations
# include the y-side and mixed versions explicitly: all derivable from the
# x-side relations + invertibility, but giving them upfront saves SGrobner
# from rediscovering them via S-polynomial reductions through large sliders
for i in [1..n-2] do
  for j in [i+1..n-1] do
    if j - i >= 2 then
      Add(relations, xs[i]*xs[j] - xs[j]*xs[i]);
      Add(relations, ys[i]*ys[j] - ys[j]*ys[i]);
      Add(relations, ys[i]*xs[j] - xs[j]*ys[i]);
      Add(relations, xs[i]*ys[j] - ys[j]*xs[i]);
    else  # j = i+1
      Add(relations, xs[i]*xs[j]*xs[i] - xs[j]*xs[i]*xs[j]);
      Add(relations, ys[i]*ys[j]*ys[i] - ys[j]*ys[i]*ys[j]);
    fi;
  od;
od;


# define sliders
S := EmptyPlist(n);
S[2] := q*ys[1] + (1-q)*one - xs[1];

for i in [3..n] do
  yProd := ys[1];
  for k in [2..i-1] do
    yProd := yProd * ys[k];
  od;
  xProd := xs[1];
  for k in [2..i-1] do
    xProd := xProd * xs[k];
  od;
  S[i] := (q^(i-1)*yProd - xProd)*S[i-1];
od;


# add zipper relations
Add(relations, (q*ys[2] + (1-q)*one - xs[2]
             - q*ys[1]*ys[2] + xs[1]*xs[2])*S[2]);

for i in [3..n-1] do
  yProd_from2 := ys[2];
  for k in [3..i] do
    yProd_from2 := yProd_from2 * ys[k];
  od;
  yProd_from1 := ys[1] * yProd_from2;
  xProd_from2 := xs[2];
  for k in [3..i] do
    xProd_from2 := xProd_from2 * xs[k];
  od;
  xProd_from1 := xs[1] * xProd_from2;
  Add(relations, (q^(i-1)*yProd_from2 - xProd_from2
               - q^(i-1)*yProd_from1 + xProd_from1)*S[i]);
od;


# add untwisting relations
#Add(relations, (xs[1] - t*one)*S[2]);

#for i in [3..n] do
#  xProd_reverse := xs[i-1];
#  for k in [2..i-1] do
#    xProd_reverse := xProd_reverse * xs[i-k];
#  od;
#  yProd_reverse := ys[i-1];
#  for k in [2..i-1] do
#    yProd_reverse := yProd_reverse * ys[i-k];
#  od;
  # Stephen's conjectured untwisting relations (shown to degenerate to BMW)
  # Add(relations, (xProd_reverse - q^(i-1)*yProd_reverse)*S[i]);

  # indeterminate untwisting; this only makes sense for n = 4
#  Add(relations, (xProd_reverse - s*yProd_reverse)*S[i]);
#od;


# compute
Print("Relations (GAP):\n");
for r in relations do Print(r, "\n"); od;
I := GP2NPList(relations);
Print("NP relations count: ", Length(I), "\n");
Print("NP relations:\n");
PrintNPList(I);
B := SGrobner(I);
Print("Groebner basis size: ", Length(B), "\n");

L := LMonsNP(B);
Display(DetermineGrowthQA(L,n*(n-1),true));
Display(DimQA(B,n*(n-1)));